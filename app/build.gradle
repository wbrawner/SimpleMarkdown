apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'

def keystoreProperties = new Properties()
try {
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} catch (FileNotFoundException ignored) {
    logger.warn("Unable to load keystore properties. Automatic signing won't be available")
    keystoreProperties['keyAlias'] = ""
    keystoreProperties['keyPassword'] = ""
    keystoreProperties['storeFile'] = File.createTempFile("temp", ".tmp").absolutePath
    keystoreProperties['storePassword'] = ""
}

def acraProperties = new Properties()
try {
    def acraPropertiesFile = project.file("acra.properties")
    acraProperties.load(new FileInputStream(acraPropertiesFile))
} catch (FileNotFoundException ignored) {
    logger.warn("Unable to load ACRA properties. Error reporting won't be available")
    acraProperties['url'] = ""
    acraProperties['user'] = ""
    acraProperties['pass'] = ""
}

android {
    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.1'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE-LGPL-2.1.txt'
        exclude 'META-INF/LICENSE-LGPL-3.txt'
        exclude 'META-INF/LICENSE-W3C-TEST'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
    }
    compileSdkVersion 29
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    defaultConfig {
        applicationId "com.wbrawner.simplemarkdown"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 27
        versionName "0.8.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "ENABLE_CUSTOM_CSS", "false"
        buildConfigField "String", "ACRA_URL", "\"${acraProperties['url']}\""
        buildConfigField "String", "ACRA_USER", "\"${acraProperties['user']}\""
        buildConfigField "String", "ACRA_PASS", "\"${acraProperties['pass']}\""
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            buildConfigField "boolean", "ENABLE_CUSTOM_CSS", "false"
        }
    }
    flavorDimensions "platform"
    productFlavors {
        free {
            applicationIdSuffix ".free"
            versionNameSuffix "-free"
        }
        play {}
        samsung {
            applicationId "com.wbrawner.simplemarkdown.samsung"
        }
    }
    dexOptions {
        jumboMode true
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

dependencies {
    samsungImplementation project(":IAP5Helper")
    implementation 'androidx.navigation:navigation-fragment-ktx:2.2.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.2.2'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.robolectric:robolectric:4.2.1'
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    def espresso_version = '3.2.0'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-web:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    def android_test = '1.2.0'
    androidTestImplementation "androidx.test:runner:$android_test"
    androidTestImplementation "androidx.test:rules:$android_test"
    androidTestUtil "androidx.test:orchestrator:$android_test"
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation "androidx.fragment:fragment-ktx:1.2.4"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'com.commonsware.cwac:anddown:0.3.0'
    playImplementation 'com.android.billingclient:billing:3.0.0'
    playImplementation 'com.google.firebase:firebase-core:17.4.3'
    implementation "androidx.core:core-ktx:1.3.0"
    implementation 'androidx.browser:browser:1.2.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    def coroutines_version = "1.3.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation 'eu.crydee:syllable-counter:4.0.2'

    def acraVersion = '5.7.0'
    implementation "ch.acra:acra-http:$acraVersion"
    implementation "ch.acra:acra-advanced-scheduler:$acraVersion"
}

android.productFlavors.each { flavor ->
    if (getGradle().getStartParameter().getTaskRequests().toString().toLowerCase().contains(flavor.name)
            && flavor.name == 'play') {
        apply plugin: 'com.google.gms.google-services'
    }
}
repositories {
    mavenCentral()
    jcenter()
}

jacoco {
    toolVersion = '0.8.0'
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [ '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*' ]
    def javaDebugTree = fileTree(dir: "$project.buildDir/intermediates/javac/debug/compileDebugJavaWithJavac/classes", excludes: fileFilter)
    def kotlinDebugTree = fileTree(dir: "$project.buildDir/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "$project.projectDir/src/main/java"

    sourceDirectories.setFrom(files([mainSrc]))
    classDirectories.setFrom(files([javaDebugTree, kotlinDebugTree]))
    executionData.setFrom(fileTree(dir: project.buildDir, includes: [
            'jacoco/testDebugUnitTest.exec',
            'outputs/code-coverage/connected/*coverage.ec'
    ]))
}
